/* Action created by DS 10/24/2014 */
let i (integer)
let temp (Feature)
/* temp stands for the Power Copy to Instantiate */

let rootFeat(PartFeature)
/* rootFeat is the father needed to create the Geometrical Set Result */
let repRef(VPMRepReference)
/* repRef is the it the reference in which the Action is run*/
let Destination_Geometrical_Set(OpenBodyFeature)
/* Destination_Geometrical_Set is where the PC will be instantiated*/
/* The 3 objects above are used to automate creation of a GS with the result*/

let Starting_Point(Point)

let P (Point)
/* P is used to instantiate PowerCopy(i+1) from Point created by PowerCopy(i)*/
let L1(Line)
let L2(Line)
let Plane(Plane)
let Axs(AxisSystem)
/* These items are used to rename objects generated by Power Copy
Only limitation, is that this action cannot know in advance how many objects need to be renamed so
I presume there is a Point, 2 Lines, a Plane and an Axis System */

let PC(Feature)
/* This feature is used to collect the name of the PowerCopy and use it to rename the result GS*/
let ResultGSName(String)

PC = AccessResource("Resource1","Feature")
ResultGSName=PC.Name

repRef = GetPLMOwner(Plane)
rootFeat = repRef->Find("PartFeature","",TRUE)
Destination_Geometrical_Set = new("OpenBodyFeature",ResultGSName,rootFeat)

P=Start_Point

i=1
for i while i<=Number_Of_Items
{
	/*instantiation of PC declared as Resource1*/
	temp=InstantiateTemplate("Resource1",Destination_Geometrical_Set)
	temp->SetAttributeObject("PCCurve",Curve)
	temp->SetAttributeObject("PCPlane",Plane)
	temp->SetAttributeObject("PCPoint",P)
	/*the following -if- is to make the first axis ON the Starting_Point*/
	if i==1
		temp->SetAttributeDimension("PCSpacing", 0m, "LENGTH")
	else
		temp->SetAttributeDimension("PCSpacing", Spacing, "LENGTH")
	EndModifyTemplate(temp)
	/*
	the command 
	temp->GetAttributeObject("Point.A")
	is able to retreive the object called Point.A created by the Power Copy.
	*/
	P=temp->GetAttributeObject("Point.A")
	/*
	The following lines will change names of the objects created by the Power Copy
	*/
	P.Name="PointA."+i
	Plane=temp->GetAttributeObject("Plane.A")
	Plane.Name="PlaneA."+i
	L1=temp->GetAttributeObject("Line.A")
	L1.Name="LineA."+i
	L2=temp->GetAttributeObject("Line.B")
	L2.Name="LineB."+i
	Axs=temp->GetAttributeObject("Axis System.A")
	Axs.Name="Axis System."+i
	
}